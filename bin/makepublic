#!/usr/bin/env perl

use strict;
use utf8;
use warnings;
use Getopt::Long;
use Config::General;
use URI::Escape;
use JSON;
use CIHM::Meta::REST::internalmeta;
use CIHM::Meta::REST::access;
use Data::Dumper;

my $conf = "/etc/canadiana/tdr/tdr.conf";
my $noupdate;
my $debug;

GetOptions(
    'conf:s'   => \$conf,
    'noupdate' => \$noupdate,
    'debug'    => \$debug
);

my %confighash = new Config::General( -ConfigFile => $conf, )->getall;

# Undefined if no <caccess> config block
my $accessdb;
if ( exists $confighash{access} ) {
    $accessdb = new CIHM::Meta::REST::access(
        server      => $confighash{access}{server},
        database    => $confighash{access}{database},
        type        => 'application/json',
        conf        => $conf,
        clientattrs => { timeout => 36000 },
    );
}
else {
    die "Missing <access> configuration block in config\n";
}

# Undefined if no <internalmeta> config block
my $internalmetadb;
if ( exists $confighash{internalmeta} ) {
    $internalmetadb = new CIHM::Meta::REST::internalmeta(
        server      => $confighash{internalmeta}{server},
        database    => $confighash{internalmeta}{database},
        type        => 'application/json',
        conf        => $conf,
        clientattrs => { timeout => 3600 },
    );
}
else {
    die "Missing <internalmeta> configuration block in config\n";
}

my %accessslugs;

print "Loading slugs from 'internalmeta'...\n";
$internalmetadb->type("application/json");
my $request = "/"
  . $internalmetadb->{database}
  . "/_design/tdr/_view/doctype?reduce=false&include_docs=false";
my $res =
  $internalmetadb->get( $request, {}, { deserializer => 'application/json' } );
if ( $res->code != 200 ) {
    die "doctype return code: " . $res->code . "\n";
}
foreach my $doc ( @{ $res->data->{rows} } ) {
    my $slug = $doc->{id};
    my $command =
      ( $doc->{key}->[0] eq "approved" ) ? "publish" : "unpublish";
    $accessslugs{$slug} = $command;
}

print "Looking up slugs in access database...\n";
$accessdb->type("application/json");

my $resultcount = -1;
my $bookmark;
until ( $resultcount == 0 ) {
    my $request = "/" . $accessdb->{database} . "/_find";
    my $res     = $accessdb->post(
        $request,
        {
            "selector" => {
                "slug" => {
                    '$exists' => JSON::true
                }
            },
            "limit"    => 100000,
            "fields"   => [ "_id", "slug", "public" ],
            "bookmark" => $bookmark
        },
        { deserializer => 'application/json' }
    );
    if ( $res->code != 200 ) {
        if ( defined $res->response->content ) {
            warn $res->response->content . "\n";
        }
        die "$request return code: " . $res->code . "\n";
    }
    if ( !exists $res->data->{docs} ) {
        die "Docs field missing" . Dumper( $res->data ) . "\n";
    }
    if ( exists $res->data->{bookmark} ) {
        $bookmark = $res->data->{bookmark};
    }

    $resultcount = scalar( @{ $res->data->{docs} } );
    foreach my $slugentry ( @{ $res->data->{docs} } ) {
        my $slug    = $slugentry->{slug};
        my $noid    = $slugentry->{'_id'};
        my $command = delete $accessslugs{ $slug};
        if ( !$command && $debug) {
            print "No command\n" . Dumper($slugentry);
        }
        if (
            $command
            && (
                (
                    ( $command eq 'publish' )
                    && !( exists $slugentry->{public} )
                )
                || ( ( $command eq 'unpublish' )
                    && exists $slugentry->{public} )
            )
          )
        {
            my $request2 = "/"
              . $accessdb->{database}
              . "/_design/access/_update/"
              . $command . "/"
              . uri_escape($noid);

            print "$noid == $slug --> $command\n";

            my $res2 = $accessdb->post( $request2, {},
                { deserializer => 'application/json' } );

            if ( $res2->code != 200 && $res2->code != 201 ) {
                die "CouchDB: \"$request\" return code: " . $res2->code . "\n";
            }
            else {
                print Dumper ( $res2->data ) if $debug;
            }
        }
    }
}

print "\n\nApproved slugs not found:\n";
foreach my $slug ( keys %accessslugs ) {
    if ( $accessslugs{$slug} eq "publish" ) {
        print $slug. "\n";
    }
}

print "\n\nUnapproved slugs not found:\n";
foreach my $slug ( keys %accessslugs ) {
    if ( $accessslugs{$slug} eq "unpublish" ) {
        print $slug. "\n";
    }
}

